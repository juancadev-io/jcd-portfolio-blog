---
title: "Singletons en Godot 4: 4 formas reales"
description: "Aprende a implementar Singleton en Godot 4 con 4 enfoques: Autoload, Singleton desde la Escena, Singleton automontado y Singleton Estatico."
pubDate: "2026-01-20"
updatedDate: "2026-01-20"
author: "JuancaDev"
lang: "es"
tags:
  [
    "godot4",
    "patrones-de-diseno",
    "singleton",
    "autoload",
    "programacion",
    "tutorial",
    "gdscript",
    "game-dev",
  ]
---

Cuando se estÃ¡ aprendiendo Godot, se suele escuchar que **"Singleton = Autoload"**. ğŸ¤”
Y pues tÃ©cnicamente Autoload es _la forma mÃ¡s cÃ³moda_ de tener un elemento Global, pero **Singleton como patrÃ³n** es otra cosa.

ğŸ’¡ **La diferencia clave:**
- **Singleton** es la _idea_: "quiero **una sola instancia** de algo".
- **Autoload** es la _herramienta de Godot_: "instÃ¡nciame esto al inicio y dÃ©jalo vivo todo el tiempo".

ğŸ¯ **La parte clave es esta:**

> âš ï¸ **No todo singleton deberÃ­a ser global.**  
> âœ¨ A veces lo quieres solo dentro de una escena (y que muera cuando sales).  
> ğŸ”§ A veces lo quieres persistente pero creado manualmente.  
> ğŸ“ Y a veces lo quieres estÃ¡tico solo para configuraciÃ³n en memoria

En este post armamos una demo y explicamos **4 formas reales**:

1. **Autoload (Godot)**
2. **Singleton por escena** (vive solo en el nivel y se destruye al salir)
3. **â€œAutoload manualâ€** (lo creas en Main y lo montas en el root)
4. **Singleton estÃ¡tico** (config en memoria + reset a defaults)

---
#
ğŸ’» **CÃ³digo fuente disponible:** Todo el cÃ³digo de este tutorial estÃ¡ disponible en [GitHub](https://github.com/juancadev-io/design-patterns/tree/main/singleton) para que puedas probarlo, modificarlo y aprender experimentando. Â¡Es completamente gratis! ğŸ‰
#

---
## ğŸ® Demo usada (idea)

Creamos una UI mÃ­nima con botones con el fin de poder probar cada patrÃ³n de singleton.

![Demo de Singletons en Godot 4](./img/ui-base.png)

> ğŸ’¡ **Nota:** No te preocupes, mÃ¡s adelante entenderÃ¡s cÃ³mo funciona cada uno.

---

# 1ï¸âƒ£ Autoload: el "singleton global" de Godot ğŸŒ

Autoload significa que Godot instancia el script o la escena desde el inicio de su ejecuciÃ³n y lo deja colgado en el **root** del Ã¡rbol de nodos.

âœ¨ **CaracterÃ­sticas:**

- â° Vive **siempre** mientras la app estÃ© abierta. Suele ser de lo primero que se ejecute.
- ğŸ¯ Es ideal para servicios globales como `AudioManager`, `GameManager`, `Settings`, etc.

### Ejemplo: `AudioService.gd`

```gdscript
extends Node

func _enter_tree() -> void:
	print("[AudioService] enter_tree (autoload)")

func _ready() -> void:
	print("[AudioService] ready (autoload)")

func play_ui_click() -> void:
	print("[AudioService] UI Click!")
```

y para activarlo, lo agregamos en el menÃº de **Project > Project Settings > Globals > Autoload**:

![Agregar Autoload en Godot](./img/autoload-setup.png)

y ya desde cualquier escena lo podrÃ¡s llamar y el editor de Godot te ayudarÃ¡ con el autocompletado:

```gdscript
AudioService.play_ui_click()
```

> âš ï¸ **Advertencia:** Si llegas a instanciar otro `AudioService` por error, no pasarÃ¡ nada raro, solo tendrÃ¡s dos nodos distintos.

---

# 2ï¸âƒ£ Singleton por Escena: vive solo en la escena actual ğŸ¬

ğŸ’¥ **Este es el punto que mucha gente no considera:** hay singletons que **no deberÃ­an ser globales**.

ğŸ¯ **Ideas para implementarlo asÃ­:**

- **BossFightDirector**: controla todo lo relacionado a la pelea contra un jefe. No tiene sentido que viva fuera de esa escena pero si que tenga una sola instancia.
- **RunSessions (Roguelike)**: estado de una partida en curso, manejando la vida, items, enemigos, etc.
- **PuzzleController**: si se tiene un puzzle complejo, que solo se activa en cierto nivel.
- **DialogueSession**: controla el estado de los dialogos de los NPCs en una escena, decide cual se activa, etc.

ğŸ§  **La regla mental:**

> ğŸ’¡ Si el nodo padre / la escena se destruye, ese singleton deberÃ­a desaparecer.

### Ejemplo: `BossFightDirector.gd`

```gdscript
extends Node
class_name BossFightDirector

static var instance: BossFightDirector
var phase := 1

func _enter_tree() -> void:
	if instance != null and instance != self:
		queue_free()
		print("[BossFightDirector] not created")
		return
	instance = self
	print("[BossFightDirector] created")

func _exit_tree() -> void:
	if instance == self:
		instance = null
		print("[BossFightDirector] destroyed")

func next_phase() -> int:
	phase += 1
	print("[BossFightDirector] phase -> ", phase)
	return phase
```

> ğŸ“Œ **Importante:** `class_name` Ãºnico para que se pueda usar en cualquier script y usar `instance` como acceso global.

Y en la escena del boss, lo agregamos como nodo hijo:

![BossFightDirector en la escena](./img/bossfight-scene.png)

En la imagen vemos dos **Directores**, esto con el fin de que cuando se inicie la escena, el segundo se destruya y solo quede uno.

Y asÃ­ lo llamarÃ­amos desde cualquier script:

```gdscript
func _on_btn_next_phase_pressed() -> void:
	var newPhase = BossFightDirector.instance.next_phase()
	labelPhase.text = str(newPhase)
```

o

```gdscript
if BossFightDirector.instance:
  BossFightDirector.instance.next_phase()
else:
  print("[Main] No BossFightDirector (not in boss scene)")
```

> ğŸ’¡ **Tip:** TÃ©cnicamente ambos scripts funcionan igual, pero el segundo evita errores si se llama fuera de la escena del boss.

### ğŸ” Â¿QuÃ© demostramos en la demo?

- En **Main**, intentas usar el director y no existe.
- En **BossFight**, al entrar:
  - se crea la instancia
  - si hay duplicados, se elimina el extra
- Al salir de la escena:
  - `_exit_tree()` se ejecuta
  - `instance = null`

âœ… Esto te permite tener un singleton **controlado por contexto**, sin contaminar el resto del juego.

---

# 3ï¸âƒ£ "Autoload manual": persistente, pero lo creas tÃº desde cÃ³digo y lo montas en root ğŸ”§

A veces quieres un servicio que **persista** entre escenas, pero no es bueno registrarlo como Autoload. 

ğŸ¯ **El ejemplo clÃ¡sico:** sistema de envÃ­o de analÃ­ticas
- âŒ Si el usuario no acepta tÃ©rminos â†’ no deberÃ­a existir
- âœ… Si acepta â†’ deberÃ­a vivir todo el tiempo

ğŸ“‹ **MÃ¡s ejemplos:**
- `SaveService` que solo montas cuando entras al juego (no en el menÃº inicial).
- `TelemetryService` o `NetworkService` que solo existe cuando el usuario estÃ¡ conectado.

ğŸ’¡ **La idea es simple:**
1. ğŸ—ï¸ Instancias el servicio desde la escena que se quiera (`Main`/`Boot`)
2. ğŸ”— Lo cuelgas del `get_tree().root`
3. âœ… Listo: sobrevive `change_scene`

### Ejemplo: `SaveService.gd`

```gdscript
extends Node
class_name SaveService

static var instance: SaveService

func _enter_tree() -> void:
	if instance != null and instance != self:
		queue_free()
		print("[SaveService] not created")
		return
	instance = self
	print("[SaveService] created")

func _exit_tree() -> void:
	if instance == self:
		instance = null
		print("[SaveService] destroyed")

func save_game() -> void:
	print("[SaveService] Saving...")

```
y en la escena Main lo creamos y montamos:

```gdscript
	var save_service = SaveService.new()
	save_service.name = "SaveService"
	get_tree().root.add_child(save_service)
```

y ya desde cualquier escena lo podrÃ¡s llamar igual que antes:

```gdscript
func _on_btn_save_root_service_pressed() -> void:
	if SaveService.instance:
		SaveService.instance.save_game()
	else:
		print("[Main] No SaveService (load Singleton)")
```
> âš ï¸ **Importante:** Por mÃ¡s que intentes crear otro `SaveService`, el patrÃ³n singleton evitarÃ¡ que se creen mÃºltiples instancias.
> ğŸ’¡ **RecomendaciÃ³n:** Siempre valida que exista la instancia antes de usarla.

### âœ¨ Beneficio principal
- Se comporta como un Autoload (vive todo el tiempo y si cambias de escena no se destruye)
- Pero tienes control total de **cuando** se crea (no al inicio del juego necesariamente)

---

# 4ï¸âƒ£ Singleton estÃ¡tico: configuraciÃ³n en memoria + reset a defaults ğŸ“

Hay casos donde no necesitas nodos, ni un lifecycle determinado, ni siquiera existir en el Ã¡rbol de nodos.
Solo necesitas guardar configuraciÃ³n en **memoria** y que sea accesible desde cualquier parte.

ğŸ’¡ **Esto que te acabo de describir es un singleton estÃ¡tico.**

ğŸ¯ **El mejor ejemplo:** modificadores globales o settings.

### Ejemplo: `GameConfig.gd`

```gdscript
class_name GameConfig

static var DEFAULTS := {
	"master_volume": 0.8,
	"fullscreen": false,
	"difficulty": "normal"
}

static var current := DEFAULTS.duplicate(true)

static func set_value(key: String, value) -> void:
	current[key] = value

static func get_value(key: String, fallback = null):
	return current.get(key, fallback)

static func reset_to_defaults() -> void:
	current = DEFAULTS.duplicate(true)

static func show_current_info():
	for key in current:
		print(key, " = ", current[key])

static func toggle_difficulty():
	if current["difficulty"] == "normal":
		current["difficulty"] = "hard"
	else:
		current["difficulty"] = "normal"

```
y desde cualquier script lo podrÃ¡s usar asÃ­:

```gdscript
func _on_btn_reset_config_pressed() -> void:
	GameConfig.reset_to_defaults()
	print("[Main] config reset")

func _on_btn_show_current_config() -> void:
	GameConfig.show_current_info()
```
> ğŸ” **Observa:** No extendemos `Node` ni usamos lifecycle, solo definimos variables y funciones estÃ¡ticas. 
> âœ¨ Pero igual usamos `class_name` para facilitar su uso.

### ğŸ¯ Lo importante
- Esto vive **en memoria**.
- Si cierras el juego, vuelve a defaults.
- Si quieres que al terminar una partida vuelva a la base, llamas:

```gdscript
GameConfig.reset_to_defaults()
```

> ğŸ’¾ **Nota:** Si quieres persistirlo entre sesiones, ahÃ­ sÃ­ ya hablamos de guardarlo en disco, pero de eso hablamos otro dÃ­a.

---

# ğŸ¤” Â¿CuÃ¡l usar y cuÃ¡ndo?

Todo depende del contexto y necesidades de tu juego, pero aquÃ­ un resumen rÃ¡pido:

## Autoload

âœ… Cuando el servicio debe existir siempre (audio global, input global, analytics).  
âŒ No usarlo como â€œbolsa global de variablesâ€ para todo.

## Singleton por escena

âœ… Cuando el estado solo tiene sentido en un nivel/escena (boss, puzzle, run).  
âœ… Cuando quieres que se destruya automÃ¡ticamente al salir.  
âŒ No lo uses si necesitas que persista entre escenas.

## Root manual (autoload controlado)

âœ… Cuando quieres persistencia entre escenas **pero con control de creaciÃ³n**.  
âœ… Ãštil para sistemas por â€œmodoâ€: gameplay, login, etc.  
âŒ Si siempre debe existir, mejor Autoload.

## Static config

âœ… Config y estado simple en memoria, sin nodos.  
âœ… Reset fÃ¡cil a defaults al cerrar partida o reiniciar run.  
âŒ No sirve si necesitas seÃ±ales, timers, procesamiento, o acceso al Ã¡rbol.

# ğŸ¯ ConclusiÃ³n

Godot te da autoload, y estÃ¡ bien... pero el patrÃ³n singleton es **mucho mÃ¡s que eso**.

âœ¨ Con estas 4 formas puedes adaptar el patrÃ³n a tus necesidades reales, evitando malas prÃ¡cticas como:
- âŒ Usar un autoload como "bolsa global de variables"
- âŒ Tener en memoria cosas que no deberÃ­an existir todo el tiempo

ğŸ¯ **Lo importante:** Entender el **concepto** de singleton (una sola instancia) y adaptarlo a las herramientas que Godot te da, para crear sistemas limpios y mantenibles.

ğŸ’¡ **Recuerda:** El patrÃ³n singleton es solo una pieza del rompecabezas, Ãºsalo sabiamente junto con otras prÃ¡cticas de diseÃ±o para crear sistemas complejos y optimizados.

---

Â¡Y como siempre, feliz coding! ğŸš€âœ¨